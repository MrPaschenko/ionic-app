import app.eduroam.shared.Config
import app.eduroam.shared.Libs

buildscript {

	repositories {
		google()
		gradlePluginPortal()
		mavenCentral()
	}
	dependencies {
		classpath Libs.androidGradlePlugin
		classpath Libs.googleService
		classpath Libs.crashlyticsPlugin
		classpath Libs.firebaseDistributionPlugin
		classpath Libs.Kotlin.gradlePlugin
		classpath Libs.Kotlin.serialization
	}
}

allprojects {
	repositories {
		google()
		mavenCentral()
	}
}

subprojects {
	afterEvaluate { project ->
		if (project.hasProperty('android')) {
			android {
				compileSdkVersion Config.compileSdk

				defaultConfig {
					minSdkVersion Config.minSdk
					targetSdkVersion Config.targetSdk
				}

				compileOptions {
					sourceCompatibility JavaVersion.VERSION_1_8
					targetCompatibility JavaVersion.VERSION_1_8
				}

				kotlinOptions {
					jvmTarget = JavaVersion.VERSION_1_8.toString()
				}
			}
		}
	}
	configurations {
		implementation {
			exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
		}
	}

}

task sayDone {
	doLast {
		exec {
			commandLine 'say', 'done'
		}
	}
}
